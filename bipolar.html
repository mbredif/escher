<!DOCTYPE html>
<html lang="en">
    <head>
        <title>Bipolar</title>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
    </head>
    <style>

body {
    margin: 0;
    background-color: #000;
    color: #fff;
    font-family: Monospace;
    font-size: 13px;
    line-height: 24px;
    overscroll-behavior: none;
}

a {
    position: absolute;
    top: 0px;
    width: 100%;
    padding: 10px;
    box-sizing: border-box;
    text-align: center;
    z-index: 1
}
    </style>
    <script type="x-shader/x-vertex" id="vertex-shader">
precision highp float;
in vec2 position;
void main() {
    gl_Position = vec4(position, 0.0, 1.0);
}
    </script>
    <script type="x-shader/x-fragment" id="fragment-shader">
precision highp float;
uniform vec2 p0;
uniform vec2 p1;
uniform vec4 iResolution;
uniform sampler2D tDiffuse;
uniform int grad;
uniform int warping;
uniform float zoom;

out vec4 gColor;

// based on https://github.com/rust-num/num-complex/blob/master/src/lib.rs
// Copyright 2013 The Rust Project Developers. MIT license
// Ported to GLSL by Andrei Kashcha (github.com/anvaka), available under MIT license as well.


// Returns a complex number z = 1 + i * 0.
vec2 c_one() { return vec2(1., 0.); }

// Returns a complex number z = 0 + i * 1.
vec2 c_i() { return vec2(0., 1.); }

float arg(vec2 c) {
  return atan(c.y, c.x);
}

// Returns conjugate of a complex number.
vec2 c_conj(vec2 c) {
  return vec2(c.x, -c.y);
}

vec2 c_from_polar(float r, float theta) {
  return vec2(r * cos(theta), r * sin(theta));
}

vec2 c_to_polar(vec2 c) {
  return vec2(length(c), atan(c.y, c.x));
}

// Computes `e^(c)`, where `e` is the base of the natural logarithm.
vec2 c_exp(vec2 c) {
  return c_from_polar(exp(c.x), c.y);
}


// Raises a floating point number to the complex power `c`.
vec2 c_exp(float base, vec2 c) {
  return c_from_polar(pow(base, c.x), c.y * log(base));
}

// Computes the principal value of natural logarithm of `c`.
vec2 c_ln(vec2 c) {
  vec2 polar = c_to_polar(c);
  return vec2(log(polar.x), polar.y);
}

// Returns the logarithm of `c` with respect to an arbitrary base.
vec2 c_log(vec2 c, float base) {
  vec2 polar = c_to_polar(c);
  return vec2(log(polar.r), polar.y) / log(base);
}

// Computes the square root of complex number `c`.
vec2 c_sqrt(vec2 c) {
  vec2 p = c_to_polar(c);
  return c_from_polar(sqrt(p.x), p.y/2.);
}

// Raises `c` to a floating point power `e`.
vec2 c_pow(vec2 c, float e) {
  vec2 p = c_to_polar(c);
  return c_from_polar(pow(p.x, e), p.y*e);
}

// Raises `c` to a complex power `e`.
vec2 c_pow(vec2 c, vec2 e) {
  vec2 polar = c_to_polar(c);
  return c_from_polar(
     pow(polar.x, e.x) * exp(-e.y * polar.y),
     e.x * polar.y + e.y * log(polar.x)
  );
}

// Computes the complex product of `self * other`.
vec2 c_mul(vec2 self, vec2 other) {
    return vec2(self.x * other.x - self.y * other.y, 
                self.x * other.y + self.y * other.x);
}

vec2 c_div(vec2 self, vec2 other) {
    float norm = length(other);
    return vec2(self.x * other.x + self.y * other.y,
                self.y * other.x - self.x * other.y)/(norm * norm);
}

vec2 c_sin(vec2 c) {
  return vec2(sin(c.x) * cosh(c.y), cos(c.x) * sinh(c.y));
}

vec2 c_cos(vec2 c) {
  // formula: cos(a + bi) = cos(a)cosh(b) - i*sin(a)sinh(b)
  return vec2(cos(c.x) * cosh(c.y), -sin(c.x) * sinh(c.y));
}

vec2 c_tan(vec2 c) {
  vec2 c2 = 2. * c;
  return vec2(sin(c2.x), sinh(c2.y))/(cos(c2.x) + cosh(c2.y));
}

vec2 c_atan(vec2 c) {
  // formula: arctan(z) = (ln(1+iz) - ln(1-iz))/(2i)
  vec2 i = c_i();
  vec2 one = c_one();
  vec2 two = one + one;
  if (c == i) {
    return vec2(0., 1./1e-10);
  } else if (c == -i) {
    return vec2(0., -1./1e-10);
  }

  return c_div(
    c_ln(one + c_mul(i, c)) - c_ln(one - c_mul(i, c)),
    c_mul(two, i)
  );
}

vec2 c_asin(vec2 c) {
 // formula: arcsin(z) = -i ln(sqrt(1-z^2) + iz)
  vec2 i = c_i(); vec2 one = c_one();
  return c_mul(-i, c_ln(
    c_sqrt(c_one() - c_mul(c, c)) + c_mul(i, c)
  ));
}

vec2 c_acos(vec2 c) {
  // formula: arccos(z) = -i ln(i sqrt(1-z^2) + z)
  vec2 i = c_i();

  return c_mul(-i, c_ln(
    c_mul(i, c_sqrt(c_one() - c_mul(c, c))) + c
  ));
}

vec2 c_sinh(vec2 c) {
  return vec2(sinh(c.x) * cos(c.y), cosh(c.x) * sin(c.y));
}

vec2 c_cosh(vec2 c) {
  return vec2(cosh(c.x) * cos(c.y), sinh(c.x) * sin(c.y));
}

vec2 c_tanh(vec2 c) {
  vec2 c2 = 2. * c;
  return vec2(sinh(c2.x), sin(c2.y))/(cosh(c2.x) + cos(c2.y));
}

vec2 c_asinh(vec2 c) {
  // formula: arcsinh(z) = ln(z + sqrt(1+z^2))
  vec2 one = c_one();
  return c_ln(c + c_sqrt(one + c_mul(c, c)));
}

vec2 c_acosh(vec2 c) {
  // formula: arccosh(z) = 2 ln(sqrt((z+1)/2) + sqrt((z-1)/2))
  vec2 one = c_one();
  vec2 two = one + one;
  return c_mul(two,
      c_ln(
        c_sqrt(c_div((c + one), two)) + c_sqrt(c_div((c - one), two))
      ));
}

vec2 c_atanh(vec2 c) {
  // formula: arctanh(z) = (ln(1+z) - ln(1-z))/2
  vec2 one = c_one();
  vec2 two = one + one;
  if (c == one) {
      return vec2(1./1e-10, vec2(0.));
  } else if (c == -one) {
      return vec2(-1./1e-10, vec2(0.));
  }
  return c_div(c_ln(one + c) - c_ln(one - c), two);
}

vec2 warp_th_bwd(in vec2 uv, inout mat2 J) {
    vec2 f = c_div(vec2(1.,0.), vec2(1.,0.) - c_mul(uv,uv));
    mat2 Jf = mat2(f.x,f.y,-f.y,f.x);
    J = Jf * J;
    uv = c_atanh(uv);
    
    return uv;
}

vec2 warp_th_fwd(in vec2 uv, inout mat2 J) {
    uv = c_tanh(uv);
    
    vec2 f = vec2(1.,0.) - c_mul(uv,uv);
    mat2 Jf = mat2(f.x,f.y,-f.y,f.x);
    J = Jf * J;
    
    return uv;
}
const float PI_2 = 1.57079632679489661923;

vec2 warp_ch_bwd(in vec2 uv, inout mat2 J) {
    vec2 f = c_div(vec2(1.,0.), sign(uv.x)*c_sqrt(c_mul(uv,uv)- vec2(1.,0.)));
    mat2 Jf = mat2(f.x,f.y,-f.y,f.x);
    J = Jf * J;
    uv = c_acosh(uv);
    
    uv = vec2(PI_2 - uv.y, uv.x);
    J = mat2(0.,1.,-1.,0.) * J;
    
    return uv;
}

vec2 warp_ch_fwd(in vec2 uv, inout mat2 J) {

    uv = vec2(uv.y, PI_2 - uv.x);
    J = mat2(0.,-1.,1.,0.) * J;
    
    vec2 f = c_sinh(uv);
    mat2 Jf = mat2(f.x,f.y,-f.y,f.x);
    J = Jf * J;
    uv = c_cosh(uv);
    
    return uv;
}

vec2 warp_sh_bwd(in vec2 uv, inout mat2 J) {
    vec2 f = c_div(vec2(1.,0.), c_sqrt(c_mul(uv,uv) + vec2(1.,0.)));
    mat2 Jf = mat2(f.x,f.y,-f.y,f.x);
    J = Jf * J;
    uv = c_asinh(uv);  
    return uv;
}

vec2 warp_sh_fwd(in vec2 uv, inout mat2 J) {
    vec2 f = c_cosh(uv);
    mat2 Jf = mat2(f.x,f.y,-f.y,f.x);
    J = Jf * J;
    uv = c_sinh(uv);
    return uv;
}

vec2 rotate_bwd(in vec2 uv, inout mat2 J) {    
    vec2 d = 0.5*(p1-p0);
    d /= dot(d,d);
    mat2 M = mat2(d.x,-d.y,d.y,d.x);
    J = M * J;
    return M * (uv - mix(p0,p1,0.5)); 
}

vec2 rotate_fwd(in vec2 uv, inout mat2 J) {    
    vec2 d = 0.5*(p1-p0);
    mat2 M = mat2(d.x,d.y,-d.y,d.x);
    J = M * J;
    return M * uv + mix(p0,p1,0.5);
}


void main()
{
    vec3 scale = vec3(iResolution.zw/iResolution.xy, 0.);

    // ndc pixel coordinates (from -1 to 1 in smallest dim)
    vec2 uv = gl_FragCoord.xy * scale.xy - 0.5 * iResolution.zw;
    // J(uv) = diag(u',v') = diag(scale)
    mat2 J = mat2(scale.xz, scale.zy);
    
    uv *= zoom;
    J *= zoom;
    
    
    if (warping == 0) {
        // no op
    } else if (warping == 1) {
    	uv = warp_ch_fwd(uv, J);
    } else if (warping == 2) {
    	uv = warp_th_fwd(uv, J);
    } else if (warping == 3) {
    	uv = warp_sh_fwd(uv, J);
    }
    
    vec2 d = 0.5*(p1-p0);
    mat2 M = mat2(d.x,d.y,-d.y,d.x);
    uv = rotate_fwd(uv, J);
    
    //gColor = texture(tDiffuse, uv);
    gColor = textureGrad(tDiffuse, uv, J[0], J[1]);
    
    //grad debug
    switch(grad) {
    case 0: break;
    case 1: J[0] = dFdx(uv);
    case 2: gColor = vec4(fract(0.5+0.5*J[0]*iResolution.xy),0.,1.); break;
    case 3: J[1] = dFdy(uv);
    case 4: gColor = vec4(fract(0.5+0.5*J[1]*iResolution.xy),0.,1.); break;
    }
    
    if (uv.x < 0. || uv.x > 1. || uv.y < 0. || uv.y > 1.) gColor = vec4(0.); // out of bounds
    if (distance(uv,p0) < 0.002 ) gColor.x = 1.; // very close to p0
    if (distance(uv,p1) < 0.002 ) gColor.y = 1.; // very close to p1
}
    </script>

    <script type="module">

        import * as THREE from './three.module.js';

        let camera, renderer, scene, material, clock;
        let textures = {};
        
        let modes, mode;
        const zoomfactor = 1.1;
        
        function init() {

            renderer = new THREE.WebGLRenderer();
            renderer.setPixelRatio( window.devicePixelRatio );
            renderer.setSize( window.innerWidth, window.innerHeight );
            document.body.appendChild( renderer.domElement );

            const loader = new THREE.TextureLoader();

            textures.vuillemin = loader.load('Carte_France_Vuillemin_1843.jpg', render);
            for (var tex in textures) {
                textures[tex].wrapS = THREE.RepeatWrapping;
                textures[tex].wrapT = THREE.RepeatWrapping;
            }
            camera = new THREE.OrthographicCamera();
            camera.position.z = 1;

            scene = new THREE.Scene();

            const geometry = new THREE.PlaneGeometry(2,2);
            material = new THREE.RawShaderMaterial({
                vertexShader: document.querySelector( '#vertex-shader' ).textContent.trim(),
                fragmentShader: document.querySelector( '#fragment-shader' ).textContent.trim(),
                uniforms: {
                    tDiffuse: { value: null },
                    p0: { value: [0,0] },
                    p1: { value: [0,0] },
                    grad: { value: 0 },
                    zoom: { value: 0 },
                    warping: { value: 2 },
                    iResolution: { value: new THREE.Vector4() }
                },
                glslVersion: THREE.GLSL3
            });
            const mesh = new THREE.Mesh( geometry, material );
            scene.add( mesh );

            clock = new THREE.Clock(false);
            clock.toggle =  function() {
                if(this.running) { 
                    this.stop();
                } else {
                    const elapsedTime = this.elapsedTime;
                    this.start();
                    this.elapsedTime = elapsedTime;
                }
            };
            
            modes = [
              { tex: "vuillemin", p1: [0.775,0.2], p0: [0.3, 0.8], zoom: 1},
            ];
            setMode(0);
            
            window.addEventListener( 'resize', onWindowResize );
            window.addEventListener( 'keydown', onKeyDown );
            onWindowResize();
        }

        function onWindowResize() {
            const w = window.innerWidth;
            const h = window.innerHeight;
            const aspect = w / h;
            renderer.setSize( w, h );
            material.uniforms.iResolution.value.x = w;
            material.uniforms.iResolution.value.y = h;
            material.uniforms.iResolution.value.z = aspect > 1 ? 2*aspect : 2;
            material.uniforms.iResolution.value.w = aspect > 1 ? 2 : 2/aspect;
	    render();
        }
        
        function setMode(m) {
            mode = (m + modes.length) % modes.length;
            material.uniforms.tDiffuse.value = textures[modes[mode].tex];
            material.uniforms.p0.value = modes[mode].p0;
            material.uniforms.p1.value = modes[mode].p1;
            material.uniforms.zoom.value = modes[mode].zoom;
	    render();
        }

	const warpings = ["none", "cosh", "tanh", "sinh"];
	
	function render() {
	        renderer.render( scene, camera );
	        document.getElementById("warping").innerHTML = warpings[material.uniforms.warping.value];
	}
	
	function onKeyDown(e) {
		if (event.defaultPrevented) return;
		switch (event.key) {
			case "ArrowLeft"  : material.uniforms.warping.value = (material.uniforms.warping.value+warpings.length-1) % warpings.length; break;
			case "ArrowRight" : material.uniforms.warping.value = (material.uniforms.warping.value+1) % warpings.length; break;
			case "ArrowUp"    : material.uniforms.zoom.value /= zoomfactor; break;
			case "ArrowDown"  : material.uniforms.zoom.value *= zoomfactor; break;
			case "g" : material.uniforms.grad.value = (material.uniforms.grad.value+1)%5; break;
			default  : return;
		}
		event.preventDefault();
	        render();
	}     
        init();

    </script>

    <body><a href="https://fr.m.wikipedia.org/wiki/Fichier:Carte_France_Vuillemin_1843.jpg">Keys: left/right (warping: sinh, none, cosh, tanh), up/down (zoom), 'g' debug gradients. Using : <span id="warping"></div></a>
    </body>
</html>
